ext {
    imVersion = "2.+"
    bioVersion = "2.+"
}

buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath group: 'org.intermine', name: 'plugin', version: '1.+'
    }
}

subprojects {
    group = 'org.intermine'
    version = '2.0.0'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'database'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
        mavenLocal()
    }

    test {
        ignoreFailures = true
    }

    configurations {
        bioModel
        mergeModel
    }

    dependencies {
        compile group: 'log4j', name: 'log4j', version: '1.2.17'
        compile group: 'org.intermine', name: 'bio-core', version: bioVersion
        compile group: 'org.intermine', name: 'intermine-model', version: imVersion
        compile group: 'org.intermine', name: 'intermine-integrate', version: imVersion
        compile group : "org.intermine", name: "intermine-resources", version: imVersion // log4j
        compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
        testCompile group: 'junit', name: 'junit', version: '4.8.2'
        testCompile group: "org.intermine", name: "intermine-integrate", version: imVersion, classifier: "testClasses"
        testCompile group: 'xmlunit', name: 'xmlunit', version: '1.0'
        bioModel group: 'org.intermine', name: 'bio-model', version: bioVersion, transitive: false
        mergeModel group : "org.intermine", name: "intermine-objectstore", version: imVersion
    }

    dbConfig {
        objectStoreName = "os.bio-test"
        modelName = "genomic"
        defaultInterminePropertiesFile = "default.intermine.bio-test.properties"
    }

    task copyMineProperties(type: Copy) {
        from "${System.env.HOME}/.intermine/intermine-bio-test.properties"
        into sourceSets.main.output.resourcesDir
        rename { fileName -> fileName.replace('intermine-bio-test.properties', 'intermine.properties') }
        inputs.sourceFiles.stopExecutionIfEmpty()
    }

    task copyBioGenomicModel(type: Copy) {
        dependsOn 'processResources'
        description "Copies the genomic_model.xml file generated by biomodel adding genomic_additions and so.obo into the output resources"
        from({ zipTree(configurations.bioModel.singleFile) }) {
            include "genomic_model.xml"
        }
        into sourceSets.main.output.resourcesDir
    }

    task copySourceProperties(type: Copy) {
        //from  project.name.minus("bio-source-") + ".properties"
        from('.') { include ("*.properties")}
        into sourceSets.main.output.resourcesDir
        inputs.sourceFiles.stopExecutionIfEmpty()
    }

    task mergeModels {
        dependsOn 'copyBioGenomicModel', 'copyMineProperties'
        doLast {
            def bioSourceAdditionsFile = project.name.minus("bio-source-") + "_additions.xml";
            if ((new File("$sourceSets.main.output.resourcesDir/" + bioSourceAdditionsFile)).exists()) {
                ant.taskdef(name: "mergeBioSourceModel", classname: "org.intermine.task.ModelMergerTask") {
                    classpath {
                        pathelement(path: configurations.mergeModel.asPath)
                        dirset(dir: buildDir)
                    }
                }
                ant.mergeBioSourceModel(inputModelFile: "$sourceSets.main.output.resourcesDir/genomic_model.xml", additionsFile: bioSourceAdditionsFile,
                        outputFile: "$sourceSets.main.output.resourcesDir/genomic_model.xml")
            }
        }
    }

    jar {
        exclude "intermine.properties"
        dependsOn 'copySourceProperties'
    }

    //only buildDB if there are tests to run
    def srcDir = "$project.projectDir" + "/src/test"
    if ((new File(srcDir)).exists()) {
        tasks.test.dependsOn 'buildUnitTestDB'
    }

    tasks.withType(Test) {
        scanForTestClasses = false
        include '**/*Test.class'
    }

    test {
        ignoreFailures = true
        forkEvery = 1
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
